/**
 *  @file kmEnvCheck.mel
 *  @brief check for env path
 */
global proc kmEnvCheck() {
	
	if(`window -ex kmEnvCheckUI`) {
		deleteUI kmEnvCheckUI;
	}
	
	int $iWinW = 700;
	int $iWinH = 200;
	
	string $windowName = `window
				-title "Check Env Path"
				-width $iWinW
				-height $iWinH
				-minimizeButton 1
				-maximizeButton 0
				// -menuBar true
				-sizeable true
			kmEnvCheckUI`;
	
	
	paneLayout
		-configuration "horizontal2" 
		-ps 1 100 80
		-ps 2 100 20
		-st 5 
	pL_envPath;
		scrollField
			-wordWrap true
			-text  ""
			// -width 500
			// -height 100
			-editable true
		sF_env;
		
		gridLayout -numberOfColumns 3 -cellWidthHeight ($iWinW/3) 20;
			button -label "MAYA_SCRIPT_PATH"		-command "kmGetPATH(\"MAYA_SCRIPT_PATH\")";
			button -label "PYTHONPATH"				-command "kmGetPATH(\"PYTHONPATH\")";
			button -label "XBMLANGPATH"				-command "kmGetPATH(\"XBMLANGPATH\")";
			button -label "MAYA_PLUG_IN_PATH"	-command "kmGetPATH(\"MAYA_PLUG_IN_PATH\")";
			button -label "MAYA_SHELF_PATH"		-command "kmGetPATH(\"MAYA_SHELF_PATH\")";
			button -label "MAYA_MODULE_PATH"	-command "kmGetPATH(\"MAYA_MODULE_PATH\")";
			button -label "MAYA_PRESET_PATH"		-command "kmGetPATH(\"MAYA_PRESET_PATH\")";
			button -label "TEMP"							-command "kmGetPATH(\"TEMP\")";
			button -label "TMPDIR"						-command "kmGetPATH(\"TMPDIR\")";
		setParent..;
	setParent..;
	
	showWindow $windowName;
}

global proc kmGetPATH( string $strEnv ) {
	string $strPath =`getenv $strEnv`;
	string $strlPath[] = stringToStringArray($strPath,";");
	
	string $strText = stringArrayToString( $strlPath ,"\n");
	
	
	scrollField -edit -text $strText sF_env;
}

